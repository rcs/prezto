#
# An expansion/customization of Steve Losh's prose theme.
#
# Authors:
#   Ryan Sorensen <rcsorensen@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/aipDQ.png

# gitish commands from https://github.com/holman/dotfiles/master/zsh/prompt.zsh

function prompt_rcs_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function git_prompt() {
  if [[ "$git_info[position]" == "" ]]; then
    echo -n ""
    return
  fi
  local position
  local extras
  position="$git_info[position]"
  extras="${(j:,:)${(s:--:)git_info[extras]}}"
  if [[ "$extras" != "" ]]; then
    extras=" ($extras)"
  fi
  if [[ "$git_info[dirty]" == "dirty" ]]; then
    position=" on %B%F{red}${(e)git_info[position]}%f%b"
  else
    position=" on ${(e)git_info[position]}"
  fi

  echo "${position}${extras}${git_info[action]}"
}

function prompt_rcs_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_rcs_precmd

  zstyle ':prezto:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':prezto:module:editor:keymap' alternate ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'
  zstyle ':prezto:module:git' action   '[%s]'
  zstyle ':prezto:module:git' ahead    '%A to push'
  zstyle ':prezto:module:git' behind   '%B behind'
  zstyle ':prezto:module:git' branch   '%b'
  zstyle ':prezto:module:git' commit   '%.7c'
  zstyle ':prezto:module:git' dirty    'dirty'
  zstyle ':prezto:module:git' position '%p'
  zstyle ':prezto:module:git' stashed  '%S stashed'
  zstyle ':prezto:module:git' unmerged '%U unmerged'
  zstyle ':prezto:module:git' info \
    'position'  '$(coalesce "%b" "%p" "%c")' \
    'extras' "%S--%A--%B--%U" \
    'action' '%s' \
    'dirty' '%D'

  PROMPT='
%F{cyan}%n%f at %F{yellow}%m%f in %F{green}%~%f$(git_prompt)
%(!.%B%F{red}#%f%b.%B%(?.%f.%F{red})❯%f%b) '
 RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_rcs_setup "$@"

